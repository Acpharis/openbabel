dnl Process this file with autoconf to produce a configure script.
AC_INIT([Open Babel], 2.0.0b1-20050601, openbabel-discuss@lists.sourceforge.net, openbabel)
AC_CONFIG_SRCDIR(src/obutil.cpp)
AM_INIT_AUTOMAKE([no-define])
AM_MAINTAINER_MODE
AM_CONFIG_HEADER(src/config.h)
AC_DEFINE_UNQUOTED([BABEL_VERSION], ["AC_PACKAGE_VERSION"], [The version of Open Babel])

AC_CANONICAL_HOST

AC_DEFINE_DIR([BABEL_DATADIR], [datadir/AC_PACKAGE_TARNAME], [Where the data files are located])

AC_DEFINE([OBAPI], [], [Used to export symbols for DLL / shared library builds])

dnl Checks for programs.
AC_PROG_CC
AC_PROG_CXX
AC_PROG_CXXCPP
AC_PROG_INSTALL
AC_PATH_PROG(DOXYGEN, doxygen)

dnl libtool
dnl You can set the shared library version in src/Makefile.am
dnl AC_DISABLE_SHARED
AC_LIBTOOL_DLOPEN
AM_PROG_LIBTOOL

dnl Complain about missing documentation tool doxygen.
AM_CONDITIONAL(BUILD_DOCS, test "$DOCTOOL" == yes)
if test "$DOCTOOL" == "no"; then
AC_MSG_WARN(Cannot find doxygen to build documentation. 
	Have a look at http://www.doxygen.org/)
fi

AC_LANG_CPLUSPLUS
dnl Checks for libraries.
AC_CHECK_LIB(m, asin, , exit)
AC_CHECK_LIB(c, dlopen, LIBDL="", [AC_CHECK_LIB(dl, dlopen, LIBDL="-ldl")])
AC_SUBST(LIBDL)
CHECK_ZLIB

dnl Checks for header files.
AC_CHECK_HEADERS(sys/time.h unistd.h stdlib.h stdio.h conio.h string.h)
AC_CHECK_HEADERS(strstream.h iostream.h fstream.h math.h time.h)
AC_CHECK_HEADERS(ctype.h iostream fstream sstream strstream)
AC_HEADER_TIME

dnl Checks for typedefs, structures, and compiler characteristics.
AC_CHECK_TYPES(clock_t)
AC_C_CONST
AC_C_INLINE

dnl Checks for library functions.
AC_CHECK_FUNCS([rint])
AC_REPLACE_FUNCS([snprintf strncasecmp])

dnl Gotta figure out whether scandir needs a const or not for the 3rd arg.
AC_MSG_CHECKING([whether scandir needs a const cast])
scandirconst_cast=no
AC_DEFINE(SCANDIR_T, , [set if scandir needs a const])

AC_COMPILE_IFELSE([
#include <sys/types.h>
#include <dirent.h>
 extern int matchFiles (struct dirent *entry_p);
 struct dirent **entries_pp;
 int count = scandir ("./", &entries_pp, $dirent_t matchFiles, 0);
], [AC_DEFINE(SCANDIR_T, [(int (*)(const dirent *))]) scandirconst_cast=yes])

AC_MSG_RESULT([$scandirconst_cast])

AC_OUTPUT(Makefile data/Makefile src/Makefile src/math/Makefile src/formats/Makefile test/Makefile test/cmltest/Makefile tools/Makefile doc/Makefile openbabel.pc)
